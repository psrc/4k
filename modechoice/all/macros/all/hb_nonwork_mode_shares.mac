~?!i&32768
~o|295
reports=hb_nonwork.rpt
~/ ---------------------------------------------------
~/ --- Home Based Nonwork Mode Choice
~/ ---------------------------------------------------
3.21
~/ ---------------------------------------------------
~/ --- Initialize Utilities
~/ ---------------------------------------------------
~+|1|y|mf"euda"|n|0|||n|2
~+|1|y|mf"eus2"|n|0|||n|2
~+|1|y|mf"eus3"|n|0|||n|2
~+|1|y|mf"eutw"|n|0|||n|2
~+|1|y|mf"eubk"|n|0|||n|2
~+|1|y|mf"euwk"|n|0|||n|2
~/ ---------------------------------------------------
~/ --- Compute exponential utility for HBNW DA mode
~/ ---------------------------------------------------
~+|1|y|mf98|n|0|||n|2
~+|1|y|mf98|n
ms"ecp1da"*mo"nwscp1"+ms"ecp2da"*mo"nwscp2"+ms"ecp3da"*mo"nwscp3"+
ms"ecp4da"*mo"nwscp4"+ms"ecp5da"*mo"nwscp5"
~+|||y|1,%1%||1,%1%||2
~/
~+|1|y|mf"euda"|n
exp(ms"nwtmau"*mf"dabtm"
+ms"nwpkca"*(md"nwhrpk"*0.5+(ms"nwvalt"/0.6)*(mf"dabcs"-mf"dabtm"))+mf98)
~+|||y|1,%1%||1,%1%||2
~/ ---------------------------------------------------
~/ --- Compute exponential utility for HBNW SR2 mode
~/ ---------------------------------------------------
~+|1|y|mf98|n|0|||n|2
~+|1|y|mf98|n
ms"ecp1s2"*mo"nwscp1"+ms"ecp2s2"*mo"nwscp2"+ms"ecp3s2"*mo"nwscp3"+
ms"ecp4s2"*mo"nwscp4"+ms"ecp5s2"*mo"nwscp5"
~+|||y|1,%1%||1,%1%||2
~/
~+|1|y|mf"eus2"|n
exp(ms"nwcos2"+ms"nwtmau"*mf"s2btm"
+ms"nwpkca"*0.50*(md"nwhrpk"*0.5+(ms"nwvalt"/0.6)*
(mf"s2bcs"-mf"s2btm"))+mf98)
~+|||y|1,%1%||1,%1%||2
~/ ---------------------------------------------------
~/ --- Compute exponential utility for HBNW SR3 mode
~/ ---------------------------------------------------
~+|1|y|mf98|n|0|||n|2
~+|1|y|mf98|n
ms"ecp1s3"*mo"nwscp1"+ms"ecp2s3"*mo"nwscp2"+ms"ecp3s3"*mo"nwscp3"+
ms"ecp4s3"*mo"nwscp4"+ms"ecp5s3"*mo"nwscp5"
~+|||y|1,%1%||1,%1%||2
~/
~+|1|y|mf"eus3"|n
exp(ms"nwcos3"+ms"nwtmau"*mf"s3btm"
+ms"nwpkca"*0.32*(md"nwhrpk"*0.5+(ms"nwvalt"/0.6)*
(mf"s3bcs"-mf"s3btm"))+mf98)
~+|||y|1,%1%||1,%1%||2
~/ -----------------------------------------------------------------
~/ --- Compute exponential utility for HBNW transit-(walk+auto) mode
~/ -----------------------------------------------------------------
~+|1|y|mf97|n|0|||n|2
~+|1|y|mf97|n
ms"nwl7"*(mf"auxwa"+mf"brdwa")+
ms"nwl7"*(mf"twtwa".min.7)+ms"nwg7"*((mf"twtwa"-7).max.0)
~+|||y|1,%1%||1,%1%||2
~/
~+|1|y|mf98|n|0|||n|2
~+|1|y|mf98|n
ms"ecp1tw"*mo"nwscp1"+ms"ecp2tw"*mo"nwscp2"+ms"ecp3tw"*mo"nwscp3"+
ms"ecp4tw"*mo"nwscp4"+ms"ecp5tw"*mo"nwscp5"
~+|||y|1,%1%||1,%1%||2
~/
~+|1|y|mf"eutw"|n
exp(ms"nwcotr"+ms"nwivtr"*mf"ivtwa"+ms"nwbdtr"*mf"nbdwa"+
mf97+ms"nwfrtr"*mf"farwa"+ms"nwedtr"*ln(1+md"empden")+mf98)
~+||mf"ivtwa"|0.001,99999,in|y|1,%1%||1,%1%||2
~/ ---------------------------------------------------
~/ --- Compute exponential utility for HBNW bike mode
~/ ---------------------------------------------------
~+|1|y|mf98|n|0|||n|2
~+|1|y|mf98|n
ms"ecp1bk"*mo"nwscp1"+ms"ecp2bk"*mo"nwscp2"+ms"ecp3bk"*mo"nwscp3"+
ms"ecp4bk"*mo"nwscp4"+ms"ecp5bk"*mo"nwscp5"
~+|||y|1,%1%||1,%1%||2
~/
~+|1|y|mf"eubk"|n
exp(ms"nwcobk"+ms"nwtmbk"*mf"bketm"+ms"nwedbk"*ln(1+md"empden")+
mf98)
~+|||y|1,%1%||1,%1%||2
~/ ---------------------------------------------------
~/ --- Compute exponential utility for HBNW walk mode
~/ ---------------------------------------------------
~+|1|y|mf98|n|0|||n|2
~+|1|y|mf98|n
ms"ecp1wk"*mo"nwscp1"+ms"ecp2wk"*mo"nwscp2"+ms"ecp3wk"*mo"nwscp3"+
ms"ecp4wk"*mo"nwscp4"+ms"ecp5wk"*mo"nwscp5"
~+|||y|1,%1%||1,%1%||2
~/
~+|1|y|mf"euwk"|n
exp(ms"nwcowk"+ms"nwtmwk"*mf"wlktm"*20+ms"nwedwk"*ln(1+md"empden")+
mf98)
~+||mf"nwwkds"|0.001,10,in|y|1,%1%||1,%1%||2
~/
q
~/ ---------------------------------------------------
~/ --- Compute Mode Shares
~/ ---------------------------------------------------
3.21
~+|1|y|mf"eusm"|n|mf"euda"+mf"eus2"+mf"eus3"+mf"eutw"+mf"eubk"+mf"euwk"|||n|2 
~+|1|y|mf"nwshda"|n|mf"euda"/mf"eusm"||mf"eusm"|0,0,ex|n|2
~+|1|y|mf"nwshs2"|n|mf"eus2"/mf"eusm"||mf"eusm"|0,0,ex|n|2
~+|1|y|mf"nwshs3"|n|mf"eus3"/mf"eusm"||mf"eusm"|0,0,ex|n|2
~+|1|y|mf"nwshtw"|n|mf"eutw"/mf"eusm"||mf"eusm"|0,0,ex|n|2
~+|1|y|mf"nwshbk"|n|mf"eubk"/mf"eusm"||mf"eusm"|0,0,ex|n|2
~+|1|y|mf"nwshwk"|n|mf"euwk"/mf"eusm"||mf"eusm"|0,0,ex|n|2
q
~/ Home-Based Non-Work Travel Mode Utility Calculation Completed!
reports=
~/exit emme
~/
q
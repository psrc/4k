~?!i&32768
~o|295
reports=nhb_mode_Shares.rpt
~/
~/ --- Start to compute exponentiated utilities for "Base" household category
~/
~/ --- Initialize full matrices of NHB utilities to default zero
3.21
~+|1|y|mf"euda"|n|0|||n|2
~+|1|y|mf"eus2"|n|0|||n|2
~+|1|y|mf"eus3"|n|0|||n|2
~+|1|y|mf"eutw"|n|0|||n|2
~+|1|y|mf"eubk"|n|0|||n|2
~+|1|y|mf"euwk"|n|0|||n|2
~+|1|y|ms"nhbvat"|n|0.6*ms"nhbtau"/ms"nhbpkc"|||2
~/ ---------------------------------------------------
~/ --- Compute exponential utility for NHB DA mode
~/ ---------------------------------------------------
~+|1|y|mf"euda"|n
exp(ms"nhbtau"*mf"mdsbtm"
+ms"nhbpkc"*(md"nwhrpk"*0.5+(ms"nhbvat"/0.6)*(mf"mdsbcs"-mf"mdsbtm")))
~+|||y|1,%1%||1,%1%||2
~/ ---------------------------------------------------
~/ --- Compute exponential utility for NHB SR2 mode
~/ ---------------------------------------------------
~+|1|y|mf"eus2"|n
exp(ms"nhbcs2"+ms"nhbtau"*mf"md2btm"
+ms"nhbpkc"*0.50*(md"nwhrpk"*0.5+(ms"nhbvat"/0.6)*(mf"md2bcs"-mf"md2btm")))
~+|||y|1,%1%||1,%1%||2
~/ ---------------------------------------------------
~/ --- Compute exponential utility for NHB SR3 mode
~/ ---------------------------------------------------
~+|1|y|mf"eus3"|n
exp(ms"nhbcs3"+ms"nhbtau"*mf"md3btm"
+ms"nhbpkc"*0.32*(md"nwhrpk"*0.5+(ms"nhbvat"/0.6)*(mf"md3bcs"-mf"md3btm")))
~+|||y|1,%1%||1,%1%||2
~/ ----------------------------------------------------------------
~/ --- Compute exponential utility for NHB transit-(walk+auto) mode
~/ ----------------------------------------------------------------
~+|1|y|mf97|n|0|||n|2
~+|1|y|mf97|n
ms"nwl7"*(mf"oauxwa"+mf"obrdwa")+ms"nwl7"*(mf"otwtwa".min.7)+
ms"nwg7"*((mf"otwtwa"-7).max.0)
~+|||y|1,%1%||1,%1%||2
~/
~+|1|y|mf"eutw"|n
exp(ms"nhbctr"+ms"nhbivt"*mf"oivtwa"+ms"nhbbtr"*mf"onbdwa"+
mf97+ms"nhbftr"*mf"opfarw"+ms"nhbmtr"*ln(1+md"empden"))
~+||mf"oivtwa"|0.001,99999,in|y|1,%1%||1,%1%||2
~/ ---------------------------------------------------
~/ --- Compute exponential utility for NHB bike mode
~/ ---------------------------------------------------
~+|1|y|mf"eubk"|n
exp(ms"nhbcbk"+ms"nhbtbk"*mf"nwbktm"+ms"nhbmwb"*ln(1+md"empden"))
~+||mf"nwwkds"|0.001,10,in|y|1,%1%||1,%1%||2
~/ ---------------------------------------------------
~/ --- Compute exponential utility for NHB walk mode
~/ ---------------------------------------------------
~+|1|y|mf"euwk"|n
exp(ms"nhbcwk"+ms"nhbtwk"*mf"nwwkds"*20+ms"nhbmwb"*ln(1+md"empden"))
~+||mf"nwwkds"|0.001,10,in|y|1,%1%||1,%1%||2
~/
q
~/ ---------------------------------------------------
~/ --- Calculate NHB  Mode Shares
~/ ---------------------------------------------------
3.21
~+|1|y|mf"eusm"|n|mf"euda"+mf"eus2"+mf"eus3"+mf"eutw"+mf"eubk"+mf"euwk"|||n|2 
~+|1|y|mf"nhshda"|n|mf"euda"/mf"eusm"||mf"eusm"|0,0,ex|n|2
~+|1|y|mf"nhshs2"|n|mf"eus2"/mf"eusm"||mf"eusm"|0,0,ex|n|2
~+|1|y|mf"nhshs3"|n|mf"eus3"/mf"eusm"||mf"eusm"|0,0,ex|n|2
~+|1|y|mf"nhshtw"|n|mf"eutw"/mf"eusm"||mf"eusm"|0,0,ex|n|2
~+|1|y|mf"nhshbk"|n|mf"eubk"/mf"eusm"||mf"eusm"|0,0,ex|n|2
~+|1|y|mf"nhshwk"|n|mf"euwk"/mf"eusm"||mf"eusm"|0,0,ex|n|2
q
~/
~/ NHB Travel Mode Utility Calculation Completed!
reports=
~/exit emme
~/
q